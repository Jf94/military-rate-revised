/*
* Prepare alliances
*/
use "${DIR_DATA_PROCESSED}/alliances.dta", clear

rename iso_ally iso_protector
rename iso iso_protected

rename iso_protector iso

// Sum over spending of protectors
merge m:1 iso year using "${DIR_DATA_PROCESSED}/panel.dta", nogen keep(master matched)
merge m:1 iso year using "${DIR_DATA_PROCESSED}/windfalls.dta", nogen keep(master matched)

gen n = 1
bysort iso year: egen n_allies = total(n)
replace milex = milex / n_allies
replace windfall = windfall / n_allies

*drop windfall
*rename windfall_01 windfall
collapse (sum) milex windfall, by(iso_protected year)
rename milex milex_allies
rename windfall windfall_allies
rename iso_protected iso

tempfile milex_allies
save `milex_allies', replace


/*
* Prepare rivalries
*/
use "${DIR_DATA_PROCESSED}/rivalries.dta", clear
rename isoa iso_rivaled
rename isob iso

// Sum over spending of rivals
merge m:1 iso year using  "${DIR_DATA_PROCESSED}/panel.dta", nogen keep(master matched)
merge m:1 iso year using "${DIR_DATA_PROCESSED}/windfalls.dta", nogen keep(master matched)

gen n = 1
bysort iso year: egen n_rivals = total(n)
replace milex = milex / n_rivals
replace windfall = windfall / n_rivals

*drop windfall
*rename windfall_01 windfall
collapse (sum) milex windfall, by(iso_rivaled year)
rename milex milex_rivals
rename windfall windfall_rivals
rename iso_rivaled iso

tempfile milex_rivals
save `milex_rivals', replace

// Merge own spending
merge 1:1 iso year using `milex_allies', nogen keep(master matched)
merge 1:1 iso year using "${DIR_DATA_PROCESSED}/panel.dta", nogen keep(master matched)
merge m:1 iso year using "${DIR_DATA_PROCESSED}/windfalls.dta", nogen keep(master matched)
rename milex milex_own


merge m:1 year using "${DIR_DATA_PROCESSED}/gprc_global.dta", nogen keep(master matched)


replace milex_allies = 0 if milex_allies == .
replace milex_rivals = 0 if milex_rivals == .
replace windfall_allies = 0 if windfall_allies == .
replace windfall_rivals = 0 if windfall_rivals == .

bysort year: egen milex_world = total(milex_own)
bysort year: egen gdp_world = total(gdp)
gen size = gdp / gdp_world

gen milex_ally_share = milex_allies / milex_world
gen milex_rival_share = milex_rivals / milex_world


bysort iso: egen max_milex_allies = max(milex_allies)
bysort iso: egen max_milex_rivals = max(milex_rivals)
*drop if max_milex_allies == 0 | max_milex_rivals == 0


drop cid
egen cid = group(iso)
xtset cid year
gen gap = milex_rivals - milex_allies - milex_own
keep if year >= 1977


forvalues h=0/15 {
	cap drop milex_own`h'
	cap drop gap`h'
	gen milex_own`h' = f`h'.milex_own - l.milex_own
	gen gap`h' = f`h'.gap - l.gap
}


eststo: xtscc milex_own1 l.gap if milex_allies != 0 & milex_rivals != 0, fe
estadd scalar r2_within = e(r2_w)
estadd local hascfe "\checkmark"

eststo: xtscc milex_own3 l.gap if milex_allies != 0 & milex_rivals != 0, fe
estadd scalar r2_within = e(r2_w)
estadd local hascfe "\checkmark"

eststo: xtscc milex_own5 l.gap if milex_allies != 0 & milex_rivals != 0, fe
estadd scalar r2_within = e(r2_w)
estadd local hascfe "\checkmark"

esttab using "${DIR_DATA_EXPORTS}/gap/delta.tex", star(* 0.1 ** 0.05  *** 0.01) keep(L.gap) r2 label fragment se tex nomtitles nonumbers replace  stats(hascfe r2_within N, fmt(1 3 "%9.0fc") label("Country fixed effects" "Within \$R^2\$" "\$N\$"))
eststo clear


label var milex_allies "TotalAlliedSpending"
label var milex_rivals "TotalRivalSpending"

// Only allies
reghdfe milex_own l.milex_allies, absorb(iso year)
local r2_within = e(r2_within)
local r2_a = e(r2_a)
eststo: xtscc f.milex_own l.milex_allies i.year, fe
estadd scalar r2_within = `r2_within'
estadd scalar r2_a = `r2_a'
estadd local hascfe "\checkmark"
estadd local hasyfe "\checkmark"

// + rivals
reghdfe milex_own l.milex_allies l.milex_rivals, absorb(iso year)
local r2_within = e(r2_within)
local r2_a = e(r2_a)
eststo: xtscc f.milex_own l.milex_allies l.milex_rivals i.year, fe
estadd scalar r2_within = `r2_within'
estadd scalar r2_a = `r2_a'
estadd local hascfe "\checkmark"
estadd local hasyfe "\checkmark"

// + gdp
reghdfe milex_own l.milex_allies l.milex_rivals l.gdp, absorb(iso year)
local r2_within = e(r2_within)
local r2_a = e(r2_a)
eststo: xtscc f.milex_own l.milex_allies l.milex_rivals l.gdp i.year, fe
estadd scalar r2_within = `r2_within'
estadd scalar r2_a = `r2_a'
estadd local hascfe "\checkmark"
estadd local hasyfe "\checkmark"


esttab using "${DIR_DATA_EXPORTS}/gap/levels.tex", star(* 0.1 ** 0.05  *** 0.01) keep(L.milex_allies L.milex_rivals L.gdp) r2 label fragment se tex nomtitles nonumbers replace  stats(hascfe r2_within r2_a N, fmt(1 3 3 "%9.0fc") label("Country fixed effects" "Within \$R^2\$" "Adj. \$R^2\$" "\$N\$"))

eststo clear


gen milex_own_gdp = milex_own / gdp
gen milex_allies_gdp = milex_allies / gdp
gen milex_rivals_gdp = milex_rivals / gdp
gen windfalls_allies_gdp = windfall_allies / gdp
gen windfalls_rivals_gdp = windfall_rivals / gdp
reghdfe f3d.milex_own_gdp l(0/3).windfalls_allies_gdp l(0/3).windfalls_rivals_gdp l(0/3).windfall_gdp l(1/3).d.milex_own_gdp, absorb(iso) cluster(iso year)